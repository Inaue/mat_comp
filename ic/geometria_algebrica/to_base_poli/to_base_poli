// ARQUIVO   : to_base_poli
// DESCRICAO : Aplicativo de calcular digitos de um polinomio em uma base de polinomio.
// AUTOR     : Inaue Ferreira da Silva
// DATA      : 30/05/2025
// VERSAO    : 1.0
// LICENCA   : GPLv3

// Definindo polinomios

ring racionais_MCs = QQ, (x), Dp;
setring racionais_MCs;

// DESCRICAO    : Divisao euclidiana de polinomios.
// PARAMETRO p1	: polinomio 1
// PARAMETRO p2	: polinomio 2
// RETORNO      : (quociente, resto)

proc divisao_euclidiana(poly p1, poly p2)
{
	poly quociente = 0;
	poly resto     = p1;
	list divisao;

	while(resto != 0 and deg(resto) >= deg(p2))
	{
		divisao   = lead(resto) / lead(p2);
		quociente = quociente + divisao[1];
		resto     = resto - divisao[1] * p2;
	}
	
	return (quociente, resto);
}

// DESCRICAO      : Funcao de imprimir digitos de um polinomio em uma base.
// PARAMETRO p    : polinomio
// PARAMETRO base : polinomio da base

proc imprima_na_base(poly p, poly base)
{
	if(p != 0)
	{
		list divisao   = divisao_euclidiana(p, base);
		poly quociente = divisao[1];
		poly resto     = divisao[2];

		imprima_na_base(quociente, base);
		print(resto);
	}
}

// Entrada

execute("poly polinomio = " + read(""));
execute("poly base      = " + read(""));

// Saida

imprima_na_base(polinomio, base);

quit;
